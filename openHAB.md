The open Home Automation Bus (openHAB) is an open source, technology agnostic home automation platform which runs as the center of your smart home. Besides 150 other add-ons for all kinds of technologies, openHAB provides an [MQTT add-on](http://docs.openhab.org/addons/bindings.html) ("binding") to interface with systems like Sonoff-Tasmota.

## openHAB Integration

After activating the MQTT binding, simply set up items for all Sonoff-Tasmota MQTT topics you are interested in. Examples are given below. Some Sonoff-Tasmota topics are JSON encoded. The `JSONPATH` transformation can be used to extract this data.

![example openHAB sitemap](https://community-openhab-org.s3-eu-central-1.amazonaws.com/original/2X/5/57750c6c7b6d9f18e75424fcb87ec093f70c6211.png)

Before continuing, please make sure you assigned unique MQTT topics in the configuration interface of each Sonoff module. The default MQTT topic is "sonoff", in the examples below we will use names like "sonoff-A00F9D". To better understand what's going on behind the scene and to be able to troubleshoot, use an [MQTT client](http://www.hivemq.com/blog/seven-best-mqtt-client-tools) in parallel.

 
### Mandatory Topics / Items

These are the minimal set of items for the basic functionality of different Sonoff modules.
<br /> (*Note: Lines have been wrapped for better presentation*)

**sonoff.items:**
```java
//Sonoff Basic / Sonoff S20 Smart Socket
Switch LivingRoom_Light "Living Room Light" <light> (LR,gLight)
    { mqtt=">[broker:cmnd/sonoff-A00F9D/power:command:*:default],
            <[broker:stat/sonoff-A00F9D/POWER:state:default]" }

// Sonoff Pow
Switch BA_Washingmachine "Washingmachine" <washer> (BA)
    { mqtt=">[broker:cmnd/sonoff-E8A6E4/power:command:*:default],
            <[broker:stat/sonoff-E8A6E4/POWER:state:default]" }
Number BA_Washingmachine_Power "Washingmachine Power [%.1f W]" (BA,gPower)
    { mqtt="<[broker:tele/sonoff-E8A6E4/ENERGY:state:JSONPATH($.Power)]" }
```

### Status Topics / Items

It is furthermore recommended, to add the following status items for every Sonoff-Tasmota device.

**sonoff.items:** 
```java
// A switch being 'ON' as long as the device is reachable
Switch LivingRoom_Light_Reachable "Living Room Light: reachable" (gReachable)
    { mqtt="<[broker:tele/sonoff-A00F9D/LWT:state:MAP(reachable.map)]" }

// Wifi Signal Strength in Percent
Number LivingRoom_Light_RSSI "Living Room Light: RSSI [%d %%]" (gRSSI)
    { mqtt="<[broker:tele/sonoff-A00F9D/STATE:state:JSONPATH($.Wifi.RSSI)]" }

// Optional! A collection of return messages by the Sonoff module
// Recommended: Define specific items for what you really need on a regular basis,
// use standalone MQTT client for troubleshooting
String LivingRoom_Light_Verbose "Living Room Light: MQTT return message [%s]"
    { mqtt="<[broker:tele/sonoff-A00F9D/INFO1:state:default],
            <[broker:stat/sonoff-A00F9D/STATUS2:state:default],
            <[broker:stat/sonoff-A00F9D/RESULT:state:default]" }
```

The "LWT" topic (["Last Will and Testament"](http://www.hivemq.com/blog/mqtt-essentials-part-9-last-will-and-testament)) will receive regular "Online" messages by the module and an "Offline" message a short time after the module is disconnected, generated by the MQTT broker. These messages are transformed to a valid `ON`/`OFF` state by the MAP transformation. The following transformation file is needed.

**reachable.map:**
```java
Online=ON
Offline=OFF
```

Of course you can implement `Unreachable` instead of `Reachable` if you prefer. 

## Maintenance Actions

A home automation system setup would not be complete without a certain maintenance automation!

Add the following elements to your openHAB setup to be able to perform actions on your Sonoff modules by the press of a simple sitemap button.

The example below includes upgrading the firmware of all modules. A shoutout to @evilgreen for the idea and a big thanks to @smadds for [providing](https://github.com/arendst/Sonoff-Tasmota/issues/19) the public firmware server used in the example.

![Sonoff Maintenance Actions](https://community-openhab-org.s3-eu-central-1.amazonaws.com/original/2X/9/97f0bdf6a81ffe94068e596804adf94839a5580b.png)

**sonoff.items:**
```java
//... all the above

//Maintenance
String	Sonoff_Action "Sonoff Action" <sonoff_basic>
```

**yourhome.sitemap:**
```java
//...
Switch item=Sonoff_Action mappings=[restart="Restart", queryFW="Query FW", upgrade="Upgrade FW"]
//...
```

**sonoff.rules:**
```java
// Work with a list of selected Sonoff modules
val sonoff_device_ids = newArrayList(
    "sonoff-A00F9D",
    //... add all your modules here!
    "sonoff-E8A6E4"
)
// OR
// Work with the grouptopic, addressing ALL modules at once
//val sonoff_device_ids = newArrayList("sonoffs")

rule "Sonoff Maintenance"
when
    Item Sonoff_Action received command
then 
    logInfo("sonoff.rules", "Sonoff Maintenance on all devices: " + receivedCommand)
    for (String device_id : sonoff_device_ids) {
        switch (receivedCommand) {
            case "restart" :
                publish("broker", "cmnd/" + device_id + "/restart", "1") 
            case "queryFW" :
                publish("broker", "cmnd/" + device_id + "/status", "2")
            case "upgrade" : {
                publish("broker", "cmnd/" + device_id + "/otaurl", "http://sonoff.maddox.co.uk/tasmota/sonoff.ino.bin")
                publish("broker", "cmnd/" + device_id + "/upgrade", "1")
            }
        }
    }
    Sonoff_Action.postUpdate(NULL)
end
```


## Community Forum

For more openHAB related details and questions, please visit the [openHAB community forum thread on Sonoff and Sonoff-Tasmota](https://community.openhab.org/t/itead-sonoff-switches-and-sockets-cheap-esp8266-wifi-mqtt-hardware/15024/1).